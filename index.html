<script src="https://cdnjs.cloudflare.com/ajax/libs/p5.js/1.3.1/p5.js"></script>
<script src="gl_utils.js"></script>
<script src="main.js"></script>
<link href="style.css" rel="stylesheet" type="text/css" />
<body bgcolor=white>
<canvas id='canvas1' width='640' height='360'>
</canvas>
</body>

<script id="vs" type="x-shader/x-vertex">
   attribute vec3 aPosition;
   varying   vec3 vPosition;
   void main() {
      gl_Position = vec4(aPosition, 1.0);
      vPosition = aPosition;
   }
</script>

<script id="fs" type="x-shader/x-fragment">
    precision mediump float;
    uniform float uTime;
    varying vec3 vPosition;
    float x;
    float y;
    float z;
    uniform vec3 uCursor;
    const int NUM_OBJECTS = 64;
    uniform vec3 cPos; // camera position
    uniform float randSeed; //cpu random seed
    uniform float thetaY;
    float seed;
    const float PI = 3.1415926535897932384626433832795;
    const int sampleCount =100;
    const float sf = 0.01;
    vec3 color = vec3(0.1,0.1,0.1);
    bool hasHit;
    struct Sphere {
        vec3 center;
        float radius;
        vec3 color;
    };

    struct Ray {
        vec3 origin;
        vec3 direction;
    };

    struct Light {
        vec3 position;
        vec3 color;
        float intens; 
    };

    struct Voxel{
      vec3 min;
      vec3 max;
      vec3 color;
      bool isInit;
      int matIndex;
    };


    Voxel voxels[128];
    Light lights[8];
    Ray rays[1];

    vec2 intersectVoxel(vec3 rayOrigin, vec3 rayDir, vec3 boxMin, vec3 boxMax) {
      vec3 tMin = (boxMin - rayOrigin) / rayDir;
      vec3 tMax = (boxMax - rayOrigin) / rayDir;
      vec3 t1 = min(tMin, tMax);
      vec3 t2 = max(tMin, tMax);
      float tNear = max(max(t1.x, t1.y), t1.z);
      float tFar = min(min(t2.x, t2.y), t2.z);
      return vec2(tNear, tFar);
    }

    vec3 getVoxelNormal(vec3 hit, Voxel voxel){
      if(abs(hit.x-voxel.min.x)  < 0.001){
        return vec3(-1,0,0);
      }
      else if(abs(hit.x-voxel.max.x)  < 0.001){
        return vec3(1,0,0);
      }
      else if(abs(hit.y-voxel.min.y)  < 0.001){
        return vec3(0,-1,0);
      }
      else if(abs(hit.y-voxel.max.y)  < 0.001){
        return vec3(0,1,0); // there is a bug here with normals being fliped into the object, fix later
      }
      else if(abs(hit.z-voxel.min.z)  < 0.001){
        return vec3(0,0,-1);
      }
      else if(abs(hit.z-voxel.max.z)  < 0.001){
        return vec3(0,0,1);
      }
      else{
        return vec3(0,0,0);
      }
    }

    vec3 rayPos(Ray ray,float t){
      return ray.origin + ray.direction*t;
    }

    mat3 rotY(float theta){
      return mat3(cos(theta),0,sin(theta),
                  0,1,0,
                  -sin(theta),0,cos(theta)
      );
    }


    void initialize() {
        x = vPosition.x;
        y = vPosition.y;
        z = vPosition.z;
        float focalLength = 1.0;
        vec3 color = vec3(0.0, 0.0, 0.0);
        // create voxels;
        voxels[0] = Voxel(vec3(-50,0,-100),vec3(50,1,50),vec3(1,1,1),true,3); //floor
        voxels[1] = Voxel(vec3(-10,-20,-10),vec3(-9,0,10),vec3(1,0,0),true,1);
        voxels[2] = Voxel(vec3(9,-20,-40),vec3(10,0,10),vec3(0,1,0),true,1);
        voxels[3] = Voxel(vec3(-4,-10.0-6.0*sin(uTime),-4),vec3(4,-9.0-6.0*sin(uTime),4),vec3(1,1,1),true,1);
        voxels[4] = Voxel(vec3(-30,-20,-41),vec3(10,0,-40),vec3(1,1,1),true,3);
        voxels[5] = Voxel(vec3(-50,-20,-100),vec3(-49,0,sin(uTime)*20.0),vec3(1,1,1),true,1);
        voxels[6] = Voxel(vec3(-50,-20,-100),vec3(-30,-18,50),vec3(1,1,1),true,1);
        voxels[7] = Voxel(vec3(-50,0,-100),vec3(0,-20,-90),vec3(1,0,0),true,2);
        voxels[8] = Voxel(vec3(-50,0,40),vec3(-30,-20,50),vec3(1,1,1),true,1);
        voxels[9] = Voxel(vec3(10,-100,0),vec3(40,0,30),vec3(1,1,1),true,1);
        voxels[10] = Voxel(vec3(cPos.x,cPos.y,cPos.z),vec3(cPos.x+2.0,cPos.y+7.0,cPos.z+2.0),vec3(1,0,0),true,1);


        // Create ray
        rays[0].origin = cPos;//vec3(0.0, 0.0, 4.0);
        rays[0].direction = rotY(thetaY) * normalize(vec3(x,-y, -focalLength));

        // Create Light source
        lights[0] =  Light(vec3(-40,-16,20),vec3(0.1,1,0.1),400.0);
        lights[1] =  Light(vec3(-40,-16,-30),vec3(1,0.1,0.1),150.0);
        lights[2] =  Light(vec3(-40,-16,-70),vec3(1,0.7,0.9),80.0);
        lights[3] =  Light(vec3(0,-14,0),vec3(1.0,0.1,10.7),400.0);
        //lights[4] =  Light(vec3(-uTime+20.0,-12.0-6.0*cos(uTime),-30),vec3(0,1.0,0.2),100.0);
    }

    vec3 worldColor(float y){
      return vec3(max((y+0.2),0.2)+0.5,max((y+0.2),0.2),max((y+0.2)*1.5,0.2))*0.8;
    }

    float random(float offset){
      return fract(sin((x+offset)*(y+2.0))*43758.5453123);
    }

    float random3(vec3 scale, float seed){
      return fract(sin(dot(gl_FragCoord.xyz + seed, scale)) * 43758.5453 + seed);
    }

    vec3 randVec(float offset){
      return vec3(random(10.0+offset)-0.5,random(20.0+offset)-0.5,random(30.0+offset)-0.5);
    }

    vec3 cosineWeightedDirection(float seed, vec3 normal) {
      float u = random3(vec3(12.9898, 78.233, 151.7182), seed);
      float v = random3(vec3(63.7264, 10.873, 623.6736), seed+1.0);
      float r = sqrt(u);
      float angle = 6.283185307179586 * v;
          // compute basis from normal
      vec3 sdir, tdir;
      if (abs(normal.x)<0.5) {
        sdir = cross(normal, vec3(1,0,0));
      } else {
        sdir = cross(normal, vec3(0,1,0));
      }
      tdir = cross(normal, sdir);
      return r*cos(angle)*sdir + r*sin(angle)*tdir + sqrt(1.-u)*normal;
    }


    vec3 mater1(Voxel hitVoxel, Ray ray,float minT){
      vec3 hitLoc = rayPos(ray,minT);
      vec3 normal = getVoxelNormal(hitLoc,hitVoxel);
      hitLoc += normal*0.01;
      vec3 gi = vec3(0.0);
      vec3 randDir = vec3(0.0);
      float minDist = 10000000.0;
      Voxel closestHit;
      for(int i = 0; i < sampleCount; i++){
        hasHit = false;
          randDir = cosineWeightedDirection(float(i)+seed*0.03, normal);//4.0*randVec(1.0/float(i)+seed) + normal;
          seed ++;
          for(int i = 0; i < 11; i++){
              vec2 hit = intersectVoxel(hitLoc,randDir,voxels[i].min,voxels[i].max);
              if(hit.x < hit.y && hit.y > 0.0){
                hasHit = true;
                if(hit.x < minDist){
                  minDist = hit.x;
                  closestHit = voxels[i];
                }
              }
          }
          if(hasHit){
            gi += closestHit.color*sf*0.03 - vec3(sf,sf,sf)*0.02;
          }
          else{
            gi += worldColor(max(-randDir.y,0.0))*sf;
          }
      }
      vec3 lighting = vec3(0.0);
      for(int j =0; j< 5; j++){
          vec3 shadowLDir = lights[j].position - hitLoc;
          vec3 originalDir = normalize(shadowLDir);
          float lDist = distance(hitLoc,lights[j].position);
          float hasHitS = 1.0;
          for(int z = 0; z< 10; z++){
            float sfs = 0.1;
            shadowLDir = lights[j].position - hitLoc + randVec(float(z)+seed);
            for(int i = 0; i < 11; i++){
              vec2 hitSL = intersectVoxel(hitLoc,shadowLDir,voxels[i].min,voxels[i].max);
              float rDist = distance(hitLoc,rayPos(Ray(hitLoc,shadowLDir),hitSL.x));
              if(hitSL.x < hitSL.y && hitSL.y > 0.01 && rDist < lDist){
                hasHitS-=sfs;
                sfs = 0.0;
              }
            }
          }
          lighting += lights[j].color * (1.0/pow(lDist,2.0)) * lights[j].intens * hasHitS * dot(normal, normalize(originalDir));
        }
      return hitVoxel.color*(gi+lighting);
    }



    vec3 mater2(Voxel hitVoxel, Ray ray, float minT){
      vec3 hitLoc = rayPos(ray,minT);
      vec3 normal = getVoxelNormal(hitLoc,hitVoxel);
      hitLoc += normal*0.01;
      vec3 reflecDir = ray.direction - 2.0*dot(ray.direction,normal)*normal + randVec(minT+uTime)*0.0;
      Voxel closestHit;
      float minDist = 10000000.0;
      for(int i = 0; i < 11; i++){
        vec2 hit = intersectVoxel(hitLoc,reflecDir,voxels[i].min,voxels[i].max);
        if(hit.x < hit.y && hit.y > 0.0){
          hasHit = true;
          if(hit.x < minDist){
            minDist = hit.x;
            closestHit = voxels[i];
          }
        }
      }
      if(hasHit){
        return mater1(closestHit,Ray(hitLoc,reflecDir),minDist)*0.9 * hitVoxel.color;
      }
      else{
        return worldColor(max(-reflecDir.y,0.1))*0.9*hitVoxel.color;
      }
    }
    

    vec3 mater3(Voxel hitVoxel, Ray ray, float minT){
      vec3 hitLoc = rayPos(ray,minT);
      float checkSize = 0.4;
      float fmodResult = mod(floor(checkSize * hitLoc.x) + floor(checkSize * hitLoc.z), 2.0);
      float fin = max(sign(fmodResult), 0.0);
      if(fin > 0.5){
        return mater1(hitVoxel,ray,minT);
      }
      else{
        return mater2(hitVoxel,ray,minT);
      }
    }

    



    
    void main() {
      initialize();
      bool hasHit = false;
      float minT = 10000.0; // holds distance to closest hit
      Voxel hitVoxel;
      seed = randSeed;
      //rays[0].direction += vec3(0.5) * vPosition.y;
      for(int i = 0; i < 10; i++){
            vec2 hit = intersectVoxel(rays[0].origin,rays[0].direction,voxels[i].min,voxels[i].max);
            if(hit.x < hit.y && hit.y > 0.0){
              if(hit.x < minT){
                minT = hit.x;
                hasHit = true;
                hitVoxel = voxels[i];
              }
            }
        }
      Voxel OhitVoxel = hitVoxel;
      if(hasHit){
        if(hitVoxel.matIndex == 1){
          gl_FragColor = vec4(mater1(hitVoxel,rays[0],minT),1.0);
        }
        else if(hitVoxel.matIndex == 2){
          gl_FragColor = vec4(mater2(hitVoxel,rays[0],minT),1.0);
        }
        else if(hitVoxel.matIndex == 3){
          gl_FragColor = vec4(mater3(hitVoxel,rays[0],minT),1.0);
        }
      }
      else{
        gl_FragColor = vec4(worldColor(max(-rays[0].direction.y,0.1)),1.0);
      }
   }
</script>

<script>
    start_gl("canvas1", getStringFromDOMElement('vs'), getStringFromDOMElement('fs'));
</script>
